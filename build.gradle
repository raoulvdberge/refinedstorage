buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.4.21"
group = "refinedstorage"
archivesBaseName = "refinedstorage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.11.2-13.20.0.2315"
    runDir = "run"
    useDepAts = true
    mappings = "snapshot_20170609"
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://maven.epoxide.org"
    }
    maven {
        url "http://maven.amadornes.com"
    }
    maven {
        url "http://maven.cil.li"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.11.2:4.5.0.287:api"
    runtime "mezz.jei:jei_1.11.2:4.5.0.287"
    deobfCompile "net.darkhax.tesla:Tesla:1.11-1.3.0.52"
    deobfCompile "MCMultiPart2:MCMultiPart-exp:2.0.0_19"
    deobfCompile "li.cil.oc:OpenComputers:MC1.11.2-1.7.0.28:api"
    compile "inventory-tweaks:InventoryTweaks:1.62:api"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    include "com/raoulvdberge/refinedstorage/**"
    include "com/jaquadro/minecraft/storagedrawers/api/**"
    include "assets/**"
    include "mcmod.info"
    include "pack.mcmeta"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

task apiJar(type: Jar, dependsOn: 'sourceMainJava') {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/raoulvdberge/refinedstorage/api/**/*'
    classifier 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
    archives sourceJar
    archives jar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            artifact deobfJar
            artifact sourceJar
            artifact apiJar
            artifact jar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'dev'
        name = 'refinedstorage'
        version {
            name = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            released = new Date()
        }
    }
}