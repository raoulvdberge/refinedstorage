buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.2.26"
group = "refinedstorage"
archivesBaseName = "refinedstorage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.10.2-12.18.3.2221"
    runDir = "run"
    useDepAts = true
    mappings = "stable_29"
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://maven.epoxide.xyz"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        url "http://maven.amadornes.com/"
    }
    maven {
        name "Cyclops Repo"
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.10.2:3.13.6.387:api"
    runtime "mezz.jei:jei_1.10.2:3.13.6.387"
    deobfCompile "net.darkhax.tesla:Tesla:1.10.2-1.2.1.50"
    deobfCompile "net.industrial-craft:industrialcraft-2:2.6.128-ex110:api"
    deobfCompile "MCMultiPart:MCMultiPart:1.3.0:universal"
    deobfCompile "org.cyclops.cyclopscore:CyclopsCore:1.9.4-0.9.1-479"
    deobfCompile "org.cyclops.commoncapabilities:CommonCapabilities:1.9.4-1.3.1-96"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
	include "com/raoulvdberge/refinedstorage/**"
	include "cofh/api/**"
	include "powercrystals/minefactoryreloaded/api/**"
	include "com/jaquadro/minecraft/storagedrawers/api/**"
    include "assets/**"
	include "ic2/**"
    include "mcmod.info"
    include "pack.mcmeta"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

task apiJar(type: Jar, dependsOn: 'sourceMainJava') {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/raoulvdberge/refinedstorage/api/**/*'
    classifier 'api'
}

artifacts {
    archives deobfJar
	archives sourceJar
    archives apiJar
    archives jar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            artifact deobfJar
			artifact sourceJar
            artifact apiJar
            artifact jar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'dev'
        name = 'refinedstorage'
        version {
            name = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            released = new Date()
        }
    }
}